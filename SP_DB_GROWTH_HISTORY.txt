CREATE PROC [dbo].[SP_DB_GROWTH_HISTORY]
	@DBNAMEPARAM SYSNAME = NULL
AS
BEGIN

	DECLARE @DBNAME SYSNAME

	/* CASO NENHUM BANCO SEJA PASSADO, TRABALHAR COM O QUE ESTÁ SELECIONADO */

	SET @DBNAME = COALESCE(@DBNAMEPARAM, DB_NAME())

	SELECT	CONVERT(CHAR, BACKUP_START_DATE, 111) AS [DATE], --YYYY/MM/DD FORMAT
			CONVERT(CHAR, BACKUP_START_DATE, 108) AS [TIME],
			@DBNAME AS [DATABASE NAME], 
			[FILEGROUP_NAME] AS [FILEGROUP NAME], 
			LOGICAL_NAME AS [LOGICAL FILENAME],
			PHYSICAL_NAME AS [PHYSICAL FILENAME], 
			CONVERT(NUMERIC(9,2),FILE_SIZE/1048576) AS [FILE SIZE (MB)],
			GROWTH AS [GROWTH PERCENTAGE (%)]
	FROM	(SELECT	B.BACKUP_START_DATE, 
					A.BACKUP_SET_ID, 
					A.FILE_SIZE, 
					A.LOGICAL_NAME, 
					A.[FILEGROUP_NAME],		
					A.PHYSICAL_NAME,
					(SELECT	CONVERT(NUMERIC(15,2),((A.FILE_SIZE * 100.00)/I1.FILE_SIZE)-100)
					 FROM	MSDB.DBO.BACKUPFILE I1
					 WHERE  I1.BACKUP_SET_ID = (SELECT	MAX(I2.BACKUP_SET_ID)
												FROM	MSDB.DBO.BACKUPFILE I2 
														INNER JOIN MSDB.DBO.BACKUPSET I3
														ON (I2.BACKUP_SET_ID = I3.BACKUP_SET_ID)
												WHERE	I2.BACKUP_SET_ID < A.BACKUP_SET_ID AND
														I2.FILE_TYPE='D' AND
														I3.DATABASE_NAME = @DBNAME AND
														I2.LOGICAL_NAME = A.LOGICAL_NAME AND
														I2.LOGICAL_NAME = I1.LOGICAL_NAME AND
														I3.TYPE = 'D') AND
							I1.FILE_TYPE = 'D') AS GROWTH
			FROM	MSDB.DBO.BACKUPFILE A 
					INNER JOIN MSDB.DBO.BACKUPSET B
					ON (A.BACKUP_SET_ID = B.BACKUP_SET_ID)
			WHERE	B.DATABASE_NAME = @DBNAME AND
					A.FILE_TYPE = 'D' AND
					B.TYPE = 'D') AS DERIVED
	WHERE	(GROWTH <> 0.0) OR 
			(GROWTH IS NULL)
	ORDER BY 
			LOGICAL_NAME, 
			[DATE]

END
